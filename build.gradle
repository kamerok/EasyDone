// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath Plugins.fabric
        classpath Plugins.gradleAndroid
        classpath Plugins.gradleVersions
        classpath Plugins.kotlin
        classpath Plugins.sqlDelight
        classpath Plugins.googleServices
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion Config.compileSdk

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk
                }

                compileOptions {
                    targetCompatibility = JavaVersion.VERSION_1_8
                    sourceCompatibility = JavaVersion.VERSION_1_8
                }

                testOptions {
                    unitTests.all {
                        reports {
                            junitXml.enabled = true
                            junitXml.destination = file("build/test-results/${project.path}")
                        }
                    }
                }
            }
        }
        try {
            test {
                reports.junitXml.destination = file("build/test-results/${project.path}/xml")
                reports.html.destination = file("build/test-results/${project.path}/html")
            }
        } catch(e) {}
    }
    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: rootProject.file('gradle/dependency_graph.gradle')
apply from: rootProject.file('gradle/dependency_updates.gradle')
